#!/usr/bin/perl -w
#
# This script sets up an application build so that it will be deployed onto a specified
# deployment environment. Please see the Fabric section of the wiki for details.
#
# This script is meant to be run after a PACKAGE has been created, which is done 
# automatically via Hudson after a change is committed to relevant application source.
#
# When run, this script copies webapp files to a central location, in a folder for a
# specified deployment environment. The application servers for that deployment
# environment will then pull those webapps and deploy them.
#
# Assumption: The database for the relevant deployment environment will be set up and/or
# upgraded to match the webapps outside the scope of this script.
#

use File::Copy;
use strict;

my $FABRIC_BUILD_DIRECTORY = "/var/shared/fabric-pipeline";
my $COMMIT_PACKAGE_FOLDER = "$FABRIC_BUILD_DIRECTORY/packages-01Commit";
my $DEPLOYMENT_ENVIRONMENT_FOLDER = "$FABRIC_BUILD_DIRECTORY/deploy";


my $USAGE=<<ENDOFUSAGE;

Usage: deploy-build --webapp <webapp> --stage <Commit> --environment <envid> [--package <package_name>]

   --webapp  Should be the basename (e.g. mom rather than mom.war)
             May be supplied more than once, to deploy multiple webapps
             Normally one of: mom, adminapp, mapmanager, previewloader

   --stage   The stage to get the release package from. That is, you are
             deploying a build that has passed the given stage.
             At the moment this can only be Commit, but stages such as
             WebTest, QA, Regression, or Stage could be added

   --environment  The deployment environment. This should be the name of a
             subfolder of $DEPLOYMENT_ENVIRONMENT_FOLDER. The webapps will
             be copied to this directory.

   --package The package name to deploy. This will be a folder under the stage
             folder. Example: trunk-2010-10-05_14-09-32-36735
             If this argument is left out, the script will find the latest package
             (for any branch, which may not be what you want). If the argument
             value ends with '-', then this is taken to be the branch, and the
             latest build for that branch will be deployed. e.g. 'trunk-' will
             deploy the latest build from trunk.


ENDOFUSAGE

my $VERBOSE = 0;
my $DEBUG = 0;

my @webapps = ();
my $STAGE = '';
my $ENVIRONMENT = '';
my $PACKAGE = '';


my @errors = ();

while (my $_ = shift @ARGV) {

	if (/^-h$/ || /^--help$/) {
		print $USAGE;
		exit 0;
	} elsif(/^-v$/ || /^--verbose$/) {
		$VERBOSE = 1;
	} elsif(/^-d$/ || /^--debug$/) {
		$DEBUG = 1;
	} elsif(/^--webapp$/ || /^-w$/) {
		push(@webapps, shift(@ARGV));
	} elsif(/^--stage$/ || /^-s$/)  {
		$STAGE = shift @ARGV;
	} elsif(/^--env/ || /^-e$/)  {
		$ENVIRONMENT = shift @ARGV;
	} elsif(/^--package/ || /^-p$/)  {
		$PACKAGE = shift @ARGV;
	} else {
		push(@errors, "Unknown argument: $_");
	} 

}

push(@errors, "No --webapps specified") unless @webapps;
push(@errors, "No --stage specified") unless $STAGE;
push(@errors, "No --environment specified") unless $ENVIRONMENT;

die "FATAL: " . join("\n", @errors) . "\n$USAGE" . "\n" if (@errors);

#
# What stage to deploy from
#

my $STAGE_FOLDER = '';
if ($STAGE eq 'Commit') {
	$STAGE_FOLDER = $COMMIT_PACKAGE_FOLDER;
} else {
	die "FATAL: Unknown stage $STAGE\n";
}

die "FATAL: Can't find directory $STAGE_FOLDER\n" unless -d $STAGE_FOLDER;
warn "Will deploy from path '$STAGE_FOLDER'\n" if $VERBOSE || $DEBUG;

#
# The package to deploy
#

if (!$PACKAGE || $PACKAGE =~ /^[\w\-]+\-$/) {
	opendir(DIR, $STAGE_FOLDER) || die "FATAL: Can't open directory '$STAGE_FOLDER': $!\n";
	my @packages = grep (!/^\./, readdir(DIR));
	closedir(DIR);
	if ($PACKAGE =~ /^([\w\-]+)\-$/) {
		my $branch = $1;
		warn "Will deploy the latest package for branch: $branch\n" if $VERBOSE || $DEBUG;
		@packages = grep(/^$branch-[\d_\-]+$/, @packages);
	}
	@packages = sort {
		my $a1 = $a;
		my $b1 = $b;
		$a1 =~ s/[-_\D]//g;
		$b1 =~ s/[-_\D]//g;
		$b1 <=> $a1;
	} @packages;
	$PACKAGE = $packages[0] || die "FATAL: No matching packages found\n";
	warn "Will deploy most recent package, which is: $PACKAGE\n";
}

my $PACKAGE_PATH = "$STAGE_FOLDER/$PACKAGE";
die "FATAL: Can't find stage package path '$PACKAGE_PATH'\n" unless -d $PACKAGE_PATH;
warn "Will deploy webapps from package '$PACKAGE_PATH'\n" if $VERBOSE || $DEBUG;

#
# The webapps to deploy
#

my $WEBAPPS_SOURCE = "$PACKAGE_PATH/wars";
die "FATAL: No webapps found in package folder '$WEBAPPS_SOURCE'\n" unless -d $WEBAPPS_SOURCE;

warn "Will attempt to deploy the following webapps: " . join(', ', @webapps) . ".\n" if $VERBOSE || $DEBUG;

my @webapp_wars = ();
foreach my $war (@webapps) {
	if (-f "$WEBAPPS_SOURCE/$war.war")  {
		push(@webapp_wars, "$WEBAPPS_SOURCE/$war.war");
	} else {
		push(@errors, "$WEBAPPS_SOURCE/$war.war");
	}
}

die "FATAL: Can't find the following war files:\n" . join("\n", @errors) . "\n" if (@errors);
warn "Will deploy the following war files:\n" . join("\n", @webapp_wars) . "\n" if $VERBOSE || $DEBUG;

#
# The environment to deploy to
#

die "FATAL: Missing folder: $DEPLOYMENT_ENVIRONMENT_FOLDER\n" unless -d $DEPLOYMENT_ENVIRONMENT_FOLDER;
my $deploy_env_dest = "$DEPLOYMENT_ENVIRONMENT_FOLDER/$ENVIRONMENT";

unless (-d $deploy_env_dest) {
	warn "WARNING: Creating folder '$deploy_env_dest'\n";
	mkdir $deploy_env_dest || die "FATAL: Failed to create $deploy_env_dest: $?\n";
}

######################################################################
# Install the package
######################################################################

#
# Make the folder and copy the wars to it
#

my $deploy_war_folder = "$deploy_env_dest/$PACKAGE";
if (-d $deploy_war_folder) {
	print "This build package is already deployed: $deploy_war_folder exists\n";
} else {

	unless ($DEBUG) {
		mkdir $deploy_war_folder || die "FATAL: Failed to create $deploy_war_folder: $?\n";
	}

	foreach my $war (@webapps) {
		warn "Copying $war.war\n" if $VERBOSE || $DEBUG;
		unless ($DEBUG) {
			copy "$WEBAPPS_SOURCE/$war.war", "$deploy_war_folder/$war.war" || die "FATAL: Could not copy $WEBAPPS_SOURCE/$war.war to $deploy_war_folder/$war.war: $?\n";
		}
	}

	#
	# Explode the static files for the mom.war for Apache
	#

	my $static_mom = "$deploy_war_folder/webapps-running";
	mkdir $static_mom || die "FATAL: Failed to create '$static_mom': $?\n";
	`unzip "$deploy_war_folder/mom.war" 'i/*' 'css/*' 'html/*' 'help/*' 'js/*' -d "$static_mom/mom"`


}

#
# Make the CURRENT symbolic link point to our new folder
#

my $current_folder = "$deploy_env_dest/CURRENT";
if (-l $current_folder) {
	warn "Removing symlink $current_folder\n" if $VERBOSE || $DEBUG;
	unless ($DEBUG) {
		unlink $current_folder || die "FATAL: Can't delete symlink $current_folder: $?\n";
	}
} elsif (-e $current_folder) {
	die "FATAL: $current_folder exists, but is not a symbolic link. Fix it!!\n";
}

warn "Symlinking $current_folder to $deploy_war_folder\n" if $VERBOSE || $DEBUG;
unless ($DEBUG) {
	symlink $deploy_war_folder, $current_folder || die "FATAL: Can't create link $current_folder to $deploy_war_folder: $?\n";
}

#
# Try to run chef-client
#

open (CHEF, "sudo /usr/bin/chef-client 2>&1 |") || die "FATAL: Can't run chef-client: $?\n";
print while(<CHEF>);
close (CHEF);

